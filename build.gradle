plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

// sourceCompatibility = JavaVersion.VERSION_8
// targetCompatibility = JavaVersion.VERSION_8

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven { url 'https://jitpack.io' }
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "com.terraformersmc:modmenu:3.2.2"

    if (Boolean.valueOf(project.use_sodium)) {
        modImplementation "com.github.CaffeineMC:sodium-fabric:mc1.18.2-0.4.1"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    } else {
        modCompileOnly("com.github.CaffeineMC:sodium-fabric:mc1.18.2-0.4.1") {
            transitive = false
        }

        modImplementation fabricApi.module("fabric-object-builder-api-v1", project.fabric_version)
        modImplementation fabricApi.module("fabric-registry-sync-v0", project.fabric_version)
        modImplementation fabricApi.module("fabric-renderer-indigo", project.fabric_version)
    }
}

loom {
    accessWidenerPath = file("src/main/resources/fxcr.accesswidener")
    mixin {
        defaultRefmapName = "mixins.fxcr.refmap.json"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        maven {
            credentials {
                username System.env["MAVEN_USERNAME"]
                password System.env["MAVEN_PASSWORD"]
            }

            def mavenUrl = System.env["MAVEN_URL"]
            url "https://${mavenUrl}/repository/maven-releases/"
        }
    }
}

apply from: 'setupRealMcAccount.gradle'